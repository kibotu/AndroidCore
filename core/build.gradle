ext {
    versionName = VERSION_NAME
    VERSION_CODE = 1
    VSC = VSC_PATH
    isCi = true
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.github.dcendents.android-maven'

android {


    compileSdkVersion compileSdkVer
    buildToolsVersion buildToolsVer

    defaultConfig {
        minSdkVersion minSdkVer
        targetSdkVersion targetSdkVer
        versionCode isCi ? commitCount() : VERSION_CODE
        versionName isCi ? simpleReleaseVersionName() : VERSION_NAME
        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        testInstrumentationRunnerArguments clearPackageData: 'true'

        buildConfigField "String", "SIMPLE_VERSION_NAME", escape(simpleReleaseVersionName())
        buildConfigField "String", "CANONICAL_VERSION_NAME", escape(canonicalReleaseVersionName())
        buildConfigField "String", "COMMIT_HASH", escape(commitHash())
        buildConfigField "String", "BRANCH", escape(branchNameByGit())
        buildConfigField "String", "BUILD_DATE", escape(new Date().getTime())

        // to avoid lib compile for release when app is compiled for debug: //http://stackoverflow.com/a/30134955/2736039
        defaultPublishConfig 'release'
        publishNonDefault true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // region compile options

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // endregion

    // region lint options

    lintOptions {
        disable 'InvalidPackage'
        abortOnError false        // true by default
        checkAllWarnings false
        checkReleaseBuilds false
        ignoreWarnings true       // false by default
        quiet true                // false by default
        tasks.lint.enabled = false
    }

    // endregion

    // region dex options

    dexOptions {
        javaMaxHeapSize "4g"
        maxProcessCount 8
    }

    // endregion

    // region adb options

    adbOptions {
        timeOutInMs 10 * 60 * 1000 // 10 minutes
    }

    // endregion

    // region exclude duplicated meta inf files

    packagingOptions {
        exclude 'META-INF/app_debug.kotlin_module'
        exclude 'META-INF/library-core_release.kotlin_module'
    }

    // endregion
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation libs.kotlinJdk8

    implementation libs.supportMultidex
    implementation libs.supportAppCompat
    implementation libs.appCompatResources
    implementation libs.constraintLayout2
    implementation libs.supportCardView

    implementation libs.androidKtxCore
    implementation libs.fragmentKtx
    implementation libs.androidKtxCollection

    implementation libs.rx2java
    implementation libs.rx2Android

    implementation libs.retrofitAdapterRxJava2
    implementation libs.retrofit
    implementation libs.okhttp3
    compileOnly libs.okhttp3LoggingInterceptor

    implementation libs.deviceInfo

    compileOnly libs.connectionBuddy
    compileOnly libs.googlePlayForceUpdate
    compileOnly libs.permissionDispatcher
    compileOnly libs.timeBomb

    implementation libs.supportRecyclerview
    implementation libs.recyclerViewPresenter

    implementation(libs.crashlytics) {
        transitive = true
    }

    implementation(libs.materialdrawer) {
        transitive = true
    }

    compileOnly libs.joda

    implementation libs.markdownview

    implementation libs.rxmarkdown

    compileOnly libs.processPhoenix

    implementation libs.logger

    compileOnly libs.playServicesAuth
    compileOnly libs.firebaseDatabase
    compileOnly libs.firebaseMessaging
    compileOnly libs.firebaseJobdispatcher
    compileOnly libs.apacheCommonsIo

    implementation libs.lifecycleExtensions
    implementation libs.lifecycleViewmodel
    implementation libs.lifecycleLivedata
    implementation libs.lifecycleRactiveStream
    kapt libs.lifecycleCompiler

    implementation libs.parceler
    kapt libs.parcelerProcessor

    compileOnly libs.realmExtensions
    compileOnly libs.gson

    implementation libs.secureStorage

    compileOnly libs.okhttp3ResponseEcho
    compileOnly libs.androidAnimationsActions

    compileOnly libs.koinAndroidxScope
    compileOnly libs.koinAndroidxViewmodel
}

// region override support library version

configurations.all {

    resolutionStrategy.cacheDynamicVersionsFor 10, 'minutes'
}

// endregion

static def escape(Object value) {
    return "\"$value\""
}

apply from: "${project.rootDir}/Android-Dependencies/javadoc.gradle"
apply from: "${project.rootDir}/Android-Dependencies/bintray.gradle"